.. include:: BookFormattingShortcuts.rst

.. _ObjectCreation2015:

.. rst-class:: SearchTitle

Object Creation
===============
 
.. rst-class:: SearchDescription

This lesson covers creating archetypes and using those archetypes to create objects at run-time.

Create a New Project
--------------------

:ref:`CreateNewProject2015`

Creating Objects with Code
--------------------------

Let's make a ball projectile Archetype.

* :ref:`Command<Commands2015>` : ``CreateSprite`` or :menuselection:`Create --> Create Sprite`
* In the Properties Window

  * Set Name to: ``Ball``  
  * Under Transform
    
    * Set Scale to: ``[0.2, 0.2, 0.2]``
    
  * Under Sprite

    * Set Color to: ``[R: 255, G: 0, B: 0, A: 1.00]`` , ``#FF0000``
        
    * Set SpriteSource to: ``Circle``
        
  * :ref:`Add Component RigidBody <AddComponent2015>`
    
  * :ref:`Add Component SphereCollider <AddComponent2015>`
    
  * Under SphereCollider
    
    * Set Radius to: ``0.5``
        
        
|BallSettings|

|pbr|

**Upload to the Archetype Ball**

  * Set Archetype to: ``Ball``
    
  * Upload the archetype

    |UploadBall|
    
  * Now that it is uploaded, you can delete your object

Now let's add the ButtonSpawner script    

* :ref:`Add a new ZilchScript resource<ResourceAdding2015>` named: ``ButtonSpawner``

  * Change the **ButtonSpawner** script to this code:
    
.. code-block:: as    
    
    class ButtonSpawner : ZilchComponent
    {
        function Initialize(init : CogInitializer)
        {
            Zero.Connect(Zero.Keyboard, Events.KeyDown, this.OnKeyDown);
        }

        function OnKeyDown(event : KeyboardEvent)
        {
            // When space key is pressed
            if(event.Key == Keys.Space)
            {
                // Spawn at this object's current position
                var spawnPosition = this.Owner.Transform.Translation;
                
                // Find the Ball archetype:
                var archetypeToSpawn = Archetype.Ball;
                
                // Pass the archetype and position to the Space to make our object
                this.Space.CreateAtPosition(archetypeToSpawn, spawnPosition);
            }
        }
    }

|pbr|
    
**ButtonSpawner object**

  * :ref:`Command<Commands2015>` : ``CreateSprite`` or :menuselection:`Create --> Create Sprite`

  * Under Properties

    * :ref:`Add Component ButtonSpawner <AddComponent2015>`
        
|ButtonSpawnerSettings|          
    
    * Set Name to: ``Spawner``

*  :ref:`Save your project <SavingProject2015>` and :ref:`run your game <RunTheGame2015>`

|pbr|

*  Now press the ``Space`` bar

|ButtonSpawner|

Modifying Created Objects
-------------------------

After you have created the object, you can access its components and modify their properties
Replace your **ButtonSpawner** script's **OnKeyDown** function with the following code to set the velocity so the ball will fire out.

.. code-block:: as        

    function OnKeyDown(event : KeyboardEvent)
    {
        // When space key is pressed
        if(event.Key == Keys.Space)
        {
            var spawnPosition = this.Owner.Transform.Translation;     

            // Find the Ball archetype:
            var archetypeToSpawn = Archetype.Ball;                
            
            // Pass the archetype and position to the Space to make our object
            var createdBall = this.Space.CreateAtPosition(archetypeToSpawn, spawnPosition);
            
            createdBall.RigidBody.Velocity = Real3(0,10,0);
        }
    }

*  :ref:`Save your project <SavingProject2015>` and :ref:`run your game <RunTheGame2015>`
    
|ButtonSpawnerFire|

Timed Spawning
--------------

Let's do a more complete example by making a TimedSpawner class.

* :ref:`Add a new ZilchScript resource<ResourceAdding2015>` named: ``TimedSpawner``

  * Change the **TimedSpawner** script to this code:

.. code-block:: as    
    
    class TimedSpawner : ZilchComponent
    {
        [Property]
        var WhatToSpawn : Archetype;

        function Initialize(init : CogInitializer)
        {
            // Connect to LogicUpdate event:
            Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        }

        function OnLogicUpdate(event : UpdateEvent)
        {
            var spawnPosition = this.Owner.Transform.Translation;                
             
            this.Space.CreateAtPosition(this.WhatToSpawn, spawnPosition);
        }
    }

|pbr|

* :ref:`Select the Spawner object <SelectObject2015>`

* Under Properties

  * :ref:`Remove the ButtonSpawner component <RemoveComponent2015>`
  * :ref:`Add the TimedSpawner component <AddComponent2015>`
  * Under TimedSpawner component
        
    * Set WhatToSpawn to: ``Ball``

|NoBookSingleLine| |TimedSpawnerSettingsWeb|

|NoWebBegin|
|TimedSpawnerSettingsBook|
|NoWebEnd|
        
* :ref:`Save your project <SavingProject2015>` and :ref:`run the game <RunTheGame2015>`   

|UpdateSpawner|

We have connected the OnLogicUpdate function to the LogicUpdate event so that it will be called every time game logic is updated. In the OnLogicUpdate method, we are creating an instance of the Ball. That will create a lot of balls very quickly. Instead, we want to create a ball only after a fixed period of time has passed.

Change the **TimedSpawner** code to look like this:

.. code-block:: as     

    class TimedSpawner : ZilchComponent
    {
        [Property]
        var WhatToSpawn : Archetype;
        
        [Property]
        var SpawnTime : Real = 1.0;
        
        var TimeSinceLastSpawn : Real = 0.0;
        
        function Initialize(init : CogInitializer)
        {
            // Connect to LogicUpdate event:
            Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        }

        function OnLogicUpdate(event : UpdateEvent)
        {
            // Add the amount of time that has just passed (Delta Time)
            // to the time since last spawn
            this.TimeSinceLastSpawn += event.Dt;
            
            // If enough time has passed, spawn the ball
            if (this.TimeSinceLastSpawn > this.SpawnTime)
            {
                var spawnPosition = this.Owner.Transform.Translation;                
                 
                this.Space.CreateAtPosition(this.WhatToSpawn, spawnPosition);
                
                // Need to reset the value
                this.TimeSinceLastSpawn = 0.0;
            }
        }
    }
    
* :ref:`Save your project <SavingProject2015>` and :ref:`run the game <RunTheGame2015>`   

|pbr|

|TimedSpawner|

On Your Own
-----------

Try:

* changing SpawnTime 
* creating new archetypes to spawn
* adding properties to the spawning component, for example:
  
  * number to spawn 
  * direction to fire

    
.. |BallSettings| image:: images/ObjectCreation/BallSettings.png

.. |UploadBall| image:: images/ObjectCreation/UploadBall.png

.. |ButtonSpawnerSettings| image:: images/ObjectCreation/ButtonSpawnerSettings.png 

.. |ButtonSpawner| image:: images/ObjectCreation/ButtonSpawner.gif

.. |ButtonSpawnerFire| image:: images/ObjectCreation/ButtonSpawnerFire.gif

.. |TimedSpawnerSettingsWeb| image:: images/ObjectCreation/TimedSpawnerSettings.png 

.. |TimedSpawnerSettingsBook| image:: images/ObjectCreation/TimedSpawnerSettings.png    
    :height: 3.5in

.. |UpdateSpawner| image:: images/ObjectCreation/UpdateSpawner.gif

.. |TimedSpawner| image:: images/ObjectCreation/TimedSpawner.gif

